apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.androidstudy.branch"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions.annotationProcessorOptions.includeCompileClasspath = true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"
                ]
            }
        }
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    buildTypes {
        release {
            minifyEnabled true
            manifestPlaceholders = [crashlyticsEnabled: true]
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix = '-debug'
            manifestPlaceholders = [crashlyticsEnabled: false]
        }
    }

    signingConfigs {
        debug {
            storeFile file("../keystore/debug.keystore")
            keyAlias "androiddebugkey"
            keyPassword "android"
            storePassword "android"
        }
    }

    lintOptions {
        // Eliminates UnusedResources false positives for resources used in DataBinding layouts
        checkGeneratedSources = true
        // Running lint over the debug variant is enough
        checkReleaseBuilds false
        // See lint.xml for rules configuration
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // androidX
    implementation deps.androidx.appcompat
    implementation deps.androidx.core
    implementation deps.androidx.constraintLayout
    implementation deps.androidx.material
    implementation deps.androidx.cardview
    implementation deps.androidx.recyclerview

    // Circle Indicator
    implementation deps.circleindicator

    // Circle ImageView
    implementation deps.circleimageview

    // Room Database
    implementation deps.room.runtime
    kapt deps.room.core
    kapt deps.room.roomCompiler

    // Paging
    implementation deps.paging
    implementation deps.androidx.extensions
    implementation deps.androidx.viewModelExt
    implementation deps.androidx.lifecycleExt

    // Kotlin Coroutines
    implementation deps.coroutines.core
    implementation deps.coroutines.android

    // DI
    implementation deps.di.koin
    implementation deps.di.koinvm
    implementation deps.inject

    // Networking
    implementation deps.network.core
    implementation deps.network.gson
    implementation deps.network.okhttp
    implementation deps.network.interceptor

    // Logging + Debugging
    implementation deps.timber
    debugImplementation deps.stetho
    debugImplementation deps.leakcanary

    // Firebase - [Analytics, Crashlytics, Performance]
    implementation 'com.google.firebase:firebase-analytics:17.2.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.google.firebase:firebase-perf:19.0.1'

    implementation 'com.afollestad.material-dialogs:core:3.1.1'

    // Required for local unit tests (JUnit 4 framework)
    testImplementation 'junit:junit:4.12'
    // Mockito framework
    testImplementation 'org.mockito:mockito-core:3.0.0'
    // Robolectric environment
    testImplementation 'androidx.test:core:1.2.0'

    // Required for instrumented tests
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.3.0-alpha02'
    // Hamcrest library
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    // UI testing with Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    // UI testing with UI Automator
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}

apply plugin: 'com.google.gms.google-services'